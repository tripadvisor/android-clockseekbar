task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // turn on the given issue id's
        enable 'HardcodedText','ExtraText','StringFormatInvalid','StringFormatCount',
                'StringFormatMatches','DefaultLocale','ExtraTranslation','MissingTranslation',
                'MissingQuantity', 'UnusedQuantity', 'RtlHardcoded', 'RtlSymmetry', 'RtlCompat',
                'GradleDependency', 'ContentDescription'
        // turn off checking the given issue id's
        disable 'UnusedIds', 'SelectableText', 'IconExpectedSize', 'SetJavaScriptEnabled',
                'SpUsage', 'TypographyQuotes', 'IconColors', 'ClickableViewAccessibility',
                'OldTargetApi', 'MisplacedStatement', 'Overdraw'
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // Set the severity of the given issues to fatal (which means they will be
        // checked during release builds (even if the lint target is not included)
        fatal 'NewApi', 'InlineApi'
        // Set the severity of the given issues to error
        error 'Wakelock', 'TextViewEdits', 'HardcodedText'
        // Set the severity of the given issues to warning
        warning 'ResourceAsColor'
    }
}

def clockAARFile = file('build/libs/lib.aar')
artifacts {
    archives clockAARFile
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getenv('NEXUS_RELEASE_URL')){
                authentication(userName: nexusUsername, password: System.getProperty('nexusPassword'))
            }
            snapshotRepository(url: System.getenv('NEXUS_SNAPSHOT_URL')){
                authentication(userName: nexusUsername, password: System.getProperty('nexusPassword'))
            }
            pom.project {
                groupId 'com.tripadvisor.flights'
                artifactId 'android-clockseekbar'
                name 'Android Clock Seekbar'
                packaging 'aar'
                version '0.1.3'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.intellij:annotations:12.0@jar'
    compile 'joda-time:joda-time:2.3'
}
